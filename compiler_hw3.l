

/* Definition section */
%{
    #include "compiler_hw_common.h"
    #include "y.tab.h"	/* header file generated by bison */
    extern YYSTYPE yylval;

    #define YY_NO_UNPUT
    #define YY_NO_INPUT
%}

/* Define regular expression label */
letter [a-zA-Z_]
digit [0-9]
id {letter}+({letter}|{digit})*
inumber {digit}+
fnumber ({digit}*\.{digit}+)

%x CMT
%x STR
%option yylineno

/* Rules section */
%%

"/*"        { BEGIN(CMT); }
<CMT>"*/"   { BEGIN(INITIAL); }
<CMT>\n     {;}
<CMT>.      {;}
"//".*      {;}
"\""        { BEGIN(STR);
                return '"';
            }
<STR>"\""   { BEGIN(INITIAL);
                return '"';
            }
<STR>[^\"]* { yylval.s_val = strdup(yytext);
                return STRING_LIT;
            }
"+"         { yylval.s_val = "ADD"; return ADD; }
"-"         { yylval.s_val = "SUB"; return SUB; }
"*"         { yylval.s_val = "MUL"; return MUL; }
"/"         { yylval.s_val = "QUO"; return QUO; }
"%"         { yylval.s_val = "REM"; return REM; }
"++"        { return INC; }
"--"        { return DEC; }
">"         { yylval.s_val = "GTR"; return GTR; }
"<"         { yylval.s_val = "LSS"; return LSS; }
">="        { yylval.s_val = "GEQ"; return GEQ; }
"<="        { yylval.s_val = "LEQ"; return LEQ; }
"=="        { yylval.s_val = "EQL"; return EQL; }
"!="        { yylval.s_val = "NEQ"; return NEQ; }
"="         { yylval.s_val = "ASSIGN"; return ASSIGN; }
"+="        { yylval.s_val = "ADD_ASSIGN"; return ADD_ASSIGN; }
"-="        { yylval.s_val = "SUB_ASSIGN"; return SUB_ASSIGN; }
"*="        { yylval.s_val = "MUL_ASSIGN"; return MUL_ASSIGN; }
"/="        { yylval.s_val = "QUO_ASSIGN"; return QUO_ASSIGN; }
"%="        { yylval.s_val = "REM_ASSIGN"; return REM_ASSIGN; }
"&&"        { yylval.s_val = "LAND"; return LAND; }
"||"        { yylval.s_val = "LOR"; return LOR; }
"!"         { yylval.s_val = "NOT"; return NOT; }
"("         { return '('; }
")"         { return ')'; }
"["         { return '['; }
"]"         { return ']'; }
"{"         { return '{'; }
"}"         { return '}'; }
":"         { return ':'; }
";"         { return ';'; }
","         { return ','; }
"\n"        { return NEWLINE; }
"print"     { return PRINT; }
"println"   { return PRINTLN; }
"if"        { return IF; }
"else"      { return ELSE; }
"for"       { return FOR; }
"int32"     { return INT; }
"float32"   { return FLOAT;}
"string"    { return STRING; }
"bool"      { return BOOL; }
"true"      { return TRUE_; }
"false"     { return FALSE_; }
"var"       { return VAR; }
"func"      { return FUNC; }
"package"   { return PACKAGE; }
"return"    { return RETURN; }
"switch"    { return SWITCH; }
"case"      { return CASE; }
"default"   { return DEFAULT; }
{inumber}   { yylval.i_val = atoi(yytext);
                return INT_LIT;
            }
{fnumber}   { yylval.f_val = atof(yytext);
                return FLOAT_LIT;
            }
{id}        { yylval.s_val = strdup(yytext);
                return IDENT;
            }
<<EOF>>     { static int once = 0;
                if (once++) {
                    yyterminate();
                }
                yylineno++;
                return NEWLINE;
            }
[ \t]+      {;}
.           {;}
%%
/*  C Code section */
int yywrap(void)
{
    return 1;
}